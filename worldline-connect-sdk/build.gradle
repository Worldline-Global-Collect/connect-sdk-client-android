/*
 * Copyright (c) 2022 Worldline Global Collect B.V
 */

plugins {
    alias libs.plugins.android.library
    alias libs.plugins.jetbrains.kotlin.android
    alias libs.plugins.signing
    alias libs.plugins.maven.publish
    alias libs.plugins.sonarqube
}

android {
    namespace 'com.worldine.connect.sdk.client.android.sdk'

    defaultConfig {
        minSdkVersion 21
        compileSdk 35
        targetSdkVersion 35
        versionCode 1
        versionName "7.0.4"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    lint {
        abortOnError false
        lintConfig file('lint.xml')
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation libs.play.services.wallet
    // The following dependency will be removed in the future, so its version can be ignored.
    //noinspection GradleDependency
    implementation libs.commons.lang3

    testImplementation libs.junit
    testImplementation libs.mockito.core
    testImplementation libs.robolectric
    testImplementation libs.test.core

    // RX
    implementation libs.rxjava
    implementation libs.rxkotlin
    implementation libs.rxandroid

    // Network
    implementation libs.converter.gson
    implementation libs.retrofit
    implementation libs.logging.interceptor
    implementation libs.adapter.rxjava3
    implementation libs.gson
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.release

                groupId = POM_GROUP_ID
                artifactId = POM_ARTIFACT_ID
                version = POM_VERSION
                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    url = POM_URL

                    organization {
                        name = POM_ORGANIZATION_NAME
                        url = POM_ORGANIZATION_URL
                    }

                    licenses {
                        license {
                            name = POM_LICENSE_NAME
                            url = POM_LICENSE_URL
                        }
                    }

                    developers {
                        developer {
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                            organization = POM_DEVELOPER_ORGANIZATION
                            organizationUrl = POM_DEVELOPER_ORGANIZATIONURL
                        }
                    }

                    issueManagement {
                        system = POM_ISSUEMANAGEMENT_SYSTEM
                        url = POM_ISSUEMANAGEMENT_URL
                    }

                    scm {
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEVELOPERCONNECTION
                        url = POM_SCM_URL
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
